<выражение> ::= <простое выражение> | <простое выражение> <операция отношения> <простое выражение>

<простое выражение> ::= <знак> <слагаемое> { <аддитивная операция> <слагаемое>}
<операция отношения> ::= = | <> | < | <= | >= | > | in

<слагаемое> ::= <множитель> { <мультипликативная операция> <множитель>}
<знак> ::= + | -
<аддитивная операция> ::= + | - | or

<множитель> ::= <переменная> | <константа без знака> | (<выражение>) | <обозначение функции> | <множество> | not <множитель>
<мультипликативная операция> ::= * | / | div | mod | and

<переменная> ::= <полная переменная> | <компонента переменной> | <указанная переменная>
<константа без знака> ::= <число без знака> | <строка> | <имя константы> | nil
<обозначение функции> ::= <имя функции> | <имя функции> (<фактический параметр> {, <фактический параметр>})
<множество> ::= [<список элементов>]

<полная переменная> ::= <имя переменной>
<компонента переменной> ::= <индексированная переменная> | <обозначение поля> | <буфер файла>
<указанная переменная> ::= <переменная-ссылка> ↑
<число без знака> ::= <целое без знака> | <вещественное без знака>
<строка> ::= ‘<символ> {<символ>}’
<имя константы> ::= <имя>
<имя функции> ::= <имя>
<имя переменной> ::= <имя>
<имя процедуры> ::= <имя>
<фактический параметр> ::= <выражение> | <переменная> | <имя процедуры> | <имя функции>
<список элементов> ::= <элемент> {, <элемент>} | <пусто>

<индексированная переменная> ::= <переменная-массив> [<выражение> {, <выражение>}]
<переменная-массив> ::= <переменная>
<обозначение поля> ::= <переменная-запись> . <имя поля>
<буфер файла> ::= <переменная-файл> ↑
<переменная-ссылка> ::= <переменная>
<целое без знака> ::= <цифра> {<цифра>}
<вещественное без знака> ::= <целое без знака> . <цифра> {<цифра>} | <целое без знака> .<цифра> {<цифра>}E <порядок> | <целое без знака> E <порядок>
<элемент> ::= <выражение> | <выражение> .. <выражение>

<переменная-запись> ::= <переменная>
<имя поля> ::= <имя>
<переменная-файл> ::= <переменная>
